// SPDX-License-Identifier: MIT
// Metaline Contracts (ShipNFT.sol)

pragma solidity ^0.8.0;

import "../utility/ResetableCounters.sol";
import "../core/ExtendableNFT.sol";

struct ShipNFTData {
    uint8 shipType; // = 1 transport ship, = 2 battle ship
    uint16 shipTypeID; // ship id
    uint8 grade; // ship grade
    
    uint32 minerAttr; 
    uint32 battleAttr;

    uint16 level;
}

/**
 * @dev Extension of {ExtendableNFT} that with fixed token data struct
 */
contract ShipNFT is ExtendableNFT {
    using ResetableCounters for ResetableCounters.Counter;

    /**
    * @dev emit when new token has been minted, see {ShipNFTData}
    *
    * @param to owner of new token
    * @param tokenId new token id
    * @param data token data see {ShipNFTData}
    */
    event ShipNFTMint(address indexed to, uint256 indexed tokenId, ShipNFTData data);
    
    /**
    * @dev emit when token data modified
    *
    * @param tokenId token id
    * @param data token data see {ShipNFTData}
    */
    event ShipNFTModified(uint256 indexed tokenId, ShipNFTData data);

    ResetableCounters.Counter internal _tokenIdTracker;
    
    mapping(uint256 => ShipNFTData) private _nftDatas; // token id => nft data stucture

    constructor(
        uint256 idStart,
        string memory name,
        string memory symbol,
        string memory baseTokenURI) 
        ExtendableNFT(name, symbol, baseTokenURI)
    {
       _tokenIdTracker.reset(idStart);

       _setupRole(DEFAULT_ADMIN_ROLE, _msgSender());
       _setupRole(DATA_ROLE, _msgSender());
    }

    /**
     * @dev Creates a new token for `to`, emit {ShipNFTMint}. Its token ID will be automatically
     * assigned (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE`.
     *
     * @param to new token owner address
     * @param data token data see {ShipNFTData}
     * @return new token id
     */
    function mint(address to, ShipNFTData memory data) public returns(uint256) {
        require(hasRole(MINTER_ROLE, _msgSender()), "R1");

        // We cannot just use balanceOf to create the new tokenId because tokens
        // can be burned (destroyed), so we need a separate counter.
        uint256 curID = _tokenIdTracker.current();

        _mint(to, curID);

        // Save token datas
        _nftDatas[curID] = data;

        emit ShipNFTMint(to, curID, data);

        // increase token id
        _tokenIdTracker.increment();

        return curID;
    }

    /**
     * @dev Creates a new token for `to`, emit {ShipNFTMint}. Its token ID give by caller
     * (and available on the emitted {IERC721-Transfer} event), and the token
     * URI autogenerated based on the base URI passed at construction.
     *
     * See {ERC721-_mint}.
     *
     * Requirements:
     *
     * - the caller must have the `MINTER_ROLE`.
     *
     * @param id new token id
     * @param to new token owner address
     * @param data token data see {ShipNFTData}
     * @return new token id
     */
    function mintFixedID(
        uint256 id,
        address to,
        ShipNFTData memory data
    ) public returns (uint256) {
        require(hasRole(MINTER_ROLE, _msgSender()), "R1");

        require(!_exists(id), "RE");

        _mint(to, id);

        // Save token datas
        _nftDatas[id] = data;

        emit ShipNFTMint(to, id, data);

        return id;
    }

    /**
     * @dev modify token data
     *
     * @param tokenId token id
     * @param data token data see {ShipNFTData}
     */
    function modNftData(uint256 tokenId, ShipNFTData memory data) external {
        require(hasRole(DATA_ROLE, _msgSender()), "R1");

        ShipNFTData storage wdata = _nftDatas[tokenId];
        wdata.level = data.level;

        emit ShipNFTModified(tokenId, wdata);
    }

    /**
     * @dev get token data
     *
     * @param tokenId token id
     * @param data token data see {ShipNFTData}
     */
    function getNftData(uint256 tokenId) external view returns(ShipNFTData memory data){
        require(_exists(tokenId), "T1");

        data = _nftDatas[tokenId];
    }

}